Обычно числовые данные в Ассемблере представлены в двоичной системе. [**Арифметические инструкции**](https://ravesli.com/assembler-arifmeticheskie-instruktsii/) работают с двоичными данными. Когда числа отображаются на экране или вводятся с клавиатуры, они имеют [**ASCII-форму**](https://ravesli.com/urok-35-simvolnyj-tip-dannyh-char/#toc-0).


# Представление чисел в Ассемблере

До сих пор мы конвертировали входные данные из ASCII-формы в двоичную систему для выполнения арифметических операций, а затем результат обратно в ASCII-форму. Например:


```
section .text

   global _start        ; должно быть объявлено для использования gcc

_start:                 ; сообщаем линкеру входную точку

   mov eax,'3'
   sub     eax, '0'
   
   mov ebx, '4'
   sub     ebx, '0'
   add eax, ebx
   add eax, '0'

   mov [sum], eax
   mov ecx,msg
   mov edx, len
   mov ebx,1          ; файловый дескриптор (stdout)
   mov eax,4          ; номер системного вызова (sys_write)
   int 0x80          ; вызов ядра

   mov ecx,sum
   mov edx, 1
   mov ebx,1          ; файловый дескриптор (stdout)
   mov eax,4          ; номер системного вызова (sys_write)
   int 0x80          ; вызов ядра
   
   mov eax,1          ; номер системного вызова (sys_exit)
   int 0x80          ; вызов ядра

section .data

msg db "The sum is:", 0xA,0xD
len equ $ - msg  

segment .bss

sum resb 1

```

**Результат выполнения программы:

`The sum is:   7`

Программирование на ассемблере позволяет более эффективно обрабатывать числа в двоичном виде. Десятичные числа могут быть представлены в следующих двух формах:

   - **ASCII-форма**;

   - **BCD-форма** (или **_«Двоично-десятичный код»_**).

# ASCII-представление

В ASCII-представлении десятичные числа хранятся в виде строки ASCII-символов. Есть 4 инструкции для обработки чисел в ASCII-представлении:

   - **AAA** (англ. _«**A**djust **A**fter **A**ddition»_) — настроить после добавления;

   - **AAS** (англ. _«**A**djust **A**fter **S**ubtraction»_) — настроить после вычитания;

   - **AAM** (англ. _«**A**djust **A**fter **M**ultiplication»_) — настроить после умножения;

   - **AAD** (англ. _«**A**djust **A**fter **D**ivision»_) — настроить после деления.

Эти инструкции не принимают никаких операндов и предполагают, что требуемый операнд находится в [**регистре**](https://ravesli.com/assembler-segmenty-pamyati-i-registry/) `AL`.

В следующем примере мы будем использовать инструкцию AAS:


```
section .text

   global _start        ; должно быть объявлено для использования gcc

_start:                 ; сообщаем линкеру входную точку

   sub     ah, ah
   mov     al, '9'
   sub     al, '3'
   aas
   or      al, 30h
   mov     [res], ax

   mov edx,len         ; длина сообщения
   mov ecx,msg         ; сообщение для вывода на экран
   mov ebx,1         ; файловый дескриптор (stdout)
   mov eax,4         ; номер системного вызова (sys_write)
   int 0x80         ; вызов ядра

   mov edx,1         ; длина сообщения
   mov ecx,res         ; сообщение для вывода на экран
   mov ebx,1         ; файловый дескриптор (stdout)
   mov eax,4         ; номер системного вызова (sys_write)
   int 0x80         ; вызов ядра

   mov eax,1         ; номер системного вызова (sys_exit)
   int 0x80         ; вызов ядра

section .data

msg db 'The Result is:',0xa
len equ $ - msg

section .bss

res resb 1

```

**Результат выполнения программы:

`The Result is:   6`  

# BCD-представление

Существует два типа BCD-представления:

   - распакованное BCD-представление;

   - упакованное BCD-представление.

В **распакованном BCD-представлении** каждый байт хранит двоичный эквивалент каждой десятичной цифры числа. Четыре инструкции настройки ASCII: AAA, AAS, AAM и AAD; также могут использоваться с распакованным BCD-представлением.

В **упакованном BCD-представлении** каждая цифра сохраняется с использованием 4 бит. Две десятичные цифры упаковываются в 1 байт. Есть две инструкции для обработки этих чисел:

   - **DAA** (англ. _«**D**ecimal Adjust **A**fter **A**ddition»_) — десятичная настройка после добавления;

   - **DAS** (англ. _«**D**ecimal Adjust **A**fter **S**ubtraction»_) — десятичная настройка после вычитания.

Обратите внимание, что в упакованном BCD-представлении отсутствует поддержка операций умножения и деления.

В следующей программе мы выполним операцию сложения двух 5-значных десятичных чисел и выведем на экран их сумму:


```
section .text

   global _start        ; должно быть объявлено для использования gcc

_start:                 ; сообщаем линкеру входную точку

   mov     esi, 4       ; указываем на крайнюю правую цифру
   mov     ecx, 5       ; количество цифр
   clc

add_loop:  

   mov al, [num1 + esi]
   adc al, [num2 + esi]
   aaa
   pushf
   or al, 30h
   popf

   mov [sum + esi], al
   dec esi
   loop add_loop

   mov edx,len         ; длина сообщения
   mov ecx,msg         ; сообщение для вывода на экран
   mov ebx,1         ; файловый дескриптор (stdout)
   mov eax,4         ; номер системного вызова (sys_write)
   int 0x80         ; вызов ядра

   mov edx,5         ; длина сообщения
   mov ecx,sum         ; сообщение для вывода на экран
   mov ebx,1         ; файловый дескриптор (stdout)
   mov eax,4         ; номер системного вызова (sys_write)
   int 0x80         ; вызов ядра

   mov eax,1         ; номер системного вызова (sys_exit)
   int 0x80         ; вызов ядра

section .data

msg db 'The Sum is:',0xa
len equ $ - msg

num1 db '12345'
num2 db '23456'
sum db '     '

```

**Результат выполнения программы:

`The Sum is:   35801`